buildscript {
  repositories {
    jcenter()
    maven {
      name = 'forge'
      url = 'http://files.minecraftforge.net/maven'
    }
    maven {
      name = 'gradle'
      url 'https://plugins.gradle.org/m2/'
    }
    maven {
      name = 'sonatype'
      url = 'https://oss.sonatype.org/content/groups/public'
    }
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.2.+'
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: project.fg_version, changing: true
  }
}

plugins {
//  id 'com.matthewprenger.cursegradle' version '1.1.0'
  id 'org.sonarqube' version '2.6.2'
}

apply plugin: 'net.minecraftforge.gradle'
//apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'jacoco'
apply plugin: 'eclipse'

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  testCompile('org.junit.jupiter:junit-jupiter-api:5.2.+')
  testCompile('org.junit.platform:junit-platform-launcher:1.2.+')
  testRuntime('org.junit.jupiter:junit-jupiter-engine:5.2.+')
  testCompile('org.mockito:mockito-core:2.+')
}

project.afterEvaluate {
  def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

  // configure jacoco to analyze the junitPlatformTest task
  jacoco {
    // this tool version is compatible with
    toolVersion = '0.8.1'
    applyTo junitPlatformTestTask
  }

  // create junit platform jacoco task
  project.task(type: JacocoReport, "junitPlatformJacocoReport",
  {
    sourceDirectories = files("./src/main")
    classDirectories = files("$buildDir/classes/main")
    executionData junitPlatformTestTask
  })
}

junitPlatform {
  reportsDir file("${project.buildDir}/test-results/junit-platform")
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs = [
    '-Xlint:deprecation'
  ]
}

javadoc {
  failOnError = false
}

test {
}

def mod_file = getModFile()
def short_version = getVersion('VERSION', mod_file)
version = mc_version + '-' + short_version
def display_version = short_version

if (System.getenv().BUILD_NUMBER) {
  version += '.' + System.getenv().BUILD_NUMBER
  display_version += '.' + System.getenv().BUILD_NUMBER
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

eclipse.project {
  buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
  natures 'org.eclipse.buildship.core.gradleprojectnature'
}

class Secrets {
  def data = null

  def getProperty(String key) {
    return data ? data[key] : ''
  }
}

import groovy.json.JsonSlurper

def secretFile
if (System.getenv().SECRET_FILE) {
  secretFile = file System.getenv().SECRET_FILE
} else {
  secretFile = file 'secret.json'
}

project.ext.secret = new Secrets()
if (secretFile.exists()) {
  secretFile.withReader {
    project.ext.secret.data = new JsonSlurper().parse it
  }
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    print project.forge_mappings_channel
    print project.forge_mappings_version
    
    mappings channel: project.forge_mappings_channel, version: project.forge_mappings_version
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    // accessTransformer = file('build/resources/main/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                examplemod {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
  maven { // Mantle, TCon, JEI
    name 'DVS1 Maven FS'
    url 'http://dvs1.progwml6.com/files/maven/'
  }
  maven { // CCL, CCC, NEI
    name 'Chickenbones Repo'
    url 'http://chickenbones.net/maven/'
  }
  maven { // The One Probe, EnderIO, EnderCore
    name 'tterrag'
    url 'http://maven.tterrag.com/'
  }
  maven { // Tesla
    name 'MMD'
    url 'https://maven.mcmoddev.com/'
  }
  maven { // MCMultipart
    name 'amadornes'
    url 'http://maven.amadornes.com/'
  }
  maven { // CraftTweaker, ZenScript
    name 'blamejared'
    url 'http://maven.blamejared.com/'
  }
  maven { // IC2
    name 'industrialcraft'
    url 'http://maven.ic2.player.to/'
  }
  maven { // HWYLA
    name 'tehnut'
    url 'http://tehnut.info/maven/'
  }
  maven { // CoFH
    name 'Covers Maven'
    url 'http://maven.covers1624.net/'
  }
  maven {
    name = 'CurseForge'
    url = 'https://minecraft.curseforge.com/api/maven/'
  }
  maven {
    name 'opencomputers'
    url 'http://maven.cil.li/'
  }
  maven { // Mekanism, TAIGA
    name 'jitpack'
    url 'https://jitpack.io/'
  }
  maven { // Buildcraft
    name 'buildcraft'
    url 'https://mod-buildcraft.com/maven/'
  }
  maven { // Storage Drawers
    name 'thiakil'
    url 'http://maven.thiakil.com/'
  }
  maven { // Applied Energistics
    name 'forge'
    url 'https://files.minecraftforge.net/maven/'
  }
}

dependencies {
  minecraft "net.minecraftforge:forge:${project.forge_mc_version}-${project.forge_version}"

/*  deobfProvided "com.mcmoddev:AdditionalLootTables:${mc_version}-${alt_version}"
  deobfProvided "com.mcmoddev:OreSpawn:${mc_version}-${orespawn_version}"

  // TODO: We don't use this
  deobfProvided "net.darkhax.tesla:Tesla-${mc_version}.2:${tesla_version}"

  deobfProvided ("com.github.aidancbrady:Mekanism:${mekanism_version}") {
    exclude group: 'com.mod-buildcraft'
    exclude group: 'CraftTweaker2'
    exclude group: 'MCMultiPart2'
    exclude group: 'cofh'
    exclude group: 'com.blamejared'
    exclude group: 'mezz.jei'
    exclude group: 'net.darkhax.tesla'
    exclude module: 'CTM'
    exclude module: 'Hwyla'
    exclude module: 'InventoryTweaks'
    exclude module: 'industrialcraft-2'
    exclude module: 'OpenComputers'
  }

  // TODO: We don't use this
  deobfProvided ("slimeknights.mantle:Mantle:${mc_version}-${mantle_version}") {
    exclude group: 'mezz.jei'
  }

  deobfProvided ("slimeknights:TConstruct:${mc_version}.2-${tconstruct_version}") {
    exclude group: 'mezz.jei'
  }

  deobfProvided ("constructs-armory:conarm:${mc_version}.2:${conarm_version}") {
    exclude group: 'mezz.jei'
  }

  deobfProvided "mezz.jei:jei_${mc_version}.2:${jei_version}"

  // TODO: We don't use this
  deobfProvided "codechicken:CodeChickenLib:${mc_version}.2-${ccl_version}:universal"

  deobfProvided ("codechicken:NotEnoughItems:${mc_version}.2-${nei_version}:universal") {
    exclude group: 'codechicken'
    exclude group: 'mezz.jei'
  }

  // TODO: We don't use this
  deobfProvided "MCMultiPart2:MCMultiPart:${mcmp_version}"

  deobfProvided ("mcjty.theoneprobe:TheOneProbe-${mc_version}:${mc_version}-${top_version}:api") {
    exclude group: 'cofh'
  }

  deobfProvided "portablejim:VeinMiner:${veinminer_version}"

  deobfProvided ("net.industrial-craft:industrialcraft-2:${ic2_version}:api") {
    exclude group: 'mezz.jei'
  }

  deobfProvided "CraftTweaker2:CraftTweaker2-API:${ct_api_version}"
  deobfProvided "CraftTweaker2:CraftTweaker2-MC1120-Main:${mc_version}-${ct_version}"

  deobfProvided ("mcp.mobius.waila:Hwyla:${hwyla_version}") {
    exclude group: 'mezz.jei'
  }

  deobfProvided ("cofh:CoFHCore:${mc_version}-${cofhcore_version}:universal") {
    exclude group: 'mezz.jei'
  }

  deobfProvided ("com.blamejared:MTLib:${mtlib_version}") {
    exclude group: 'mezz.jei'
  }

  deobfProvided ("thaumcraft:Thaumcraft:${mc_version}.2:6.1.BETA26") {
    exclude group: 'mezz.jei'
  }
  deobfProvided ("baubles:Baubles:${mc_version}:1.5.2"){
    exclude group: 'mezz.jei'
  }
  // TODO: Dense Ores, EnderCore, EnderIO
*/
}

processResources {
  inputs.property 'version', project.version
  inputs.property 'mcversion', project.mc_version

  from (sourceSets.main.resources.srcDirs) {
    include 'mcmod.info'
    expand 'version': short_version, 'mcversion': project.mc_version
  }

  from (sourceSets.main.resources.srcDirs) {
    exclude 'mcmod.info'
  }

  rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
      attributes([
        "Specification-Title": project.description,
        "Specification-Vendor": project.author,
        "Specification-Version": "1", // We are version 1 of ourselves
        "Implementation-Title": project.name,
        "Implementation-Version": "${version}",
        "Implementation-Vendor" : project.author,
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
      ])

    if (project.hasProperty('coreplugin')) {
      attributes 'FMLCorePluginContainsFMLMod': 'true'
      attributes 'FMLCorePlugin': project.coreplugin
    }
//  attributes 'FMLAT' : ''
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project.group
      artifactId project.archivesBaseName
      version project.version
      from components.java
    }
  }
  repositories {
    maven {
      credentials {
        username secret.username
        password secret.password
      }
      url secret.url
    }
  }
}

/*curseforge {
  if (project.hasProperty('cf_project_id') && project.hasProperty('cf_release_type') && project.hasProperty('cf_changelog_filename') &&
      ((project.cf_release_type == 'alpha') || (project.cf_release_type == 'beta') || (project.cf_release_type == 'release'))) {
    apiKey = secret.curseforgeAPIKey
    project {
      id = project.cf_project_id
      changelog = file(project.cf_changelog_filename)
      releaseType = project.cf_release_type
      addGameVersion(project.mc_version)
      mainArtifact(jar) {
        displayName = "$project.description $display_version"
        relations {
          requiredLibrary 'mmd-orespawn'
          optionalLibrary 'additional-loot-tables'
          optionalLibrary 'crafttweaker'
          optionalLibrary 'hwyla'
          optionalLibrary 'industrial-craft'
          optionalLibrary 'mekanism'
          optionalLibrary 'the-one-probe'
          optionalLibrary 'thermalexpansion'
          optionalLibrary 'tinkers-construct'
          optionalLibrary 'veinminer'
        }
      }
    }
  }
}*/

sonarqube {
  properties {
    property 'sonar.host.url', secret.sonarHost
    property 'sonar.organization', secret.sonarOrganization
    property 'sonar.login', secret.sonarToken
    property 'sonar.junit.reportPaths', "${project.buildDir}/test-results/junit-platform/*.xml"
    property 'sonar.jacoco.reportPaths', "${project.buildDir}/jacoco/junitPlatformTest.exec"
  }
}

String getModFile() {
  String path = ''
  FileTree tree = fileTree(dir: 'src/main/java')
  tree.include '**/*.java'
  tree.visit { element ->
    if (element.file.isFile()) {
      element.file.eachLine { String s ->
        s = s.trim()
        if (s.startsWith('@Mod(')) {
          path = "src/main/java/$element.relativePath"
        }
      }
    }
  }
  return path
}

String getVersion(String type, String mod_file) {
  String major = '0'
  String revision = '0'
  String patch = '0'
  File file = file(mod_file)
  def prefix = ~/^(?:public|protected|private) static final String $type = ".*$/
  file.eachLine { String s ->
    s = s.trim()
    if (s ==~ prefix || prefix.matcher(s).matches()) {
      String[] bits = s.split("=")
      String interesting = bits[1].trim()
      interesting = interesting.substring(1, interesting.length() - 2)
      String[] pts = interesting.trim().split("\\.")

      major = pts[0] ?: '0'
      revision = pts[1] ?: '0'
      patch = pts[2] ?: '0'
    }
  }
  return "$major.$revision.$patch"
}
